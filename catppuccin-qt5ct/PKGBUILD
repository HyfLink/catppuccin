depname="qt5ct"
pkgname="catppuccin-$depname"
pkgver=0.1.0
pkgrel=1
pkgdesc="catppuccin for $depname"
arch=("any")
url="https://github.com/catppuccin/$depname"
license=("MIT")
optdepends=("qt5ct" "qt6ct")
makedepends=("just" "catppuccin-whiskers-bin")
source=("git+$url.git")
sha256sums=('SKIP')

flavors=("latte" "frappe" "macchiato" "mocha")
accents=("rosewater" "flamingo" "pink" "mauve" "red" "maroon" "peach" "yellow" "green" "teal" "sky" "sapphire" "blue" "lavender")

build() {
    cd "$depname"
    just build
}

package() {
    licenseDir="$pkgdir/usr/share/licenses/catppuccin/$depname"
    install -Ddm755 $licenseDir
    install -m644 "$depname/README.md" $licenseDir
    install -m644 "$depname/LICENSE" $licenseDir

    qt5ctDir="$pkgdir/usr/share/qt5ct/colors/"
    qt6ctDir="$pkgdir/usr/share/qt6ct/colors/"
    install -Ddm755 $qt5ctDir
    install -Ddm755 $qt6ctDir

    for flavor in ${flavors[@]}; do
        for accent in ${accents[@]}; do
            install -m644 "$depname/themes/catppuccin-$flavor-$accent.conf" $qt5ctDir
            install -m644 "$depname/themes/catppuccin-$flavor-$accent.conf" $qt6ctDir
        done
    done
}
