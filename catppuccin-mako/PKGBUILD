depname="mako"
pkgname="catppuccin-$depname"
pkgver=0.1.0
pkgrel=1
pkgdesc="catppuccin for $depname"
arch=("any")
url="https://github.com/catppuccin/$depname"
license=("MIT")
optdepends=("$depname")
makedepends=("just" "catppuccin-whiskers-bin")
source=("git+$url.git")
sha256sums=('SKIP')

flavors=("latte" "frappe" "macchiato" "mocha")
accents=("rosewater" "flamingo" "pink" "mauve" "red" "maroon" "peach" "yellow" "green" "teal" "sky" "sapphire" "blue" "lavender")

build() {
    cd "$depname"
    just build
}

package() {
    licenseDir="$pkgdir/usr/share/licenses/catppuccin/$depname"
    install -Ddm755 $licenseDir
    install -m644 "$depname/README.md" $licenseDir
    install -m644 "$depname/LICENSE" $licenseDir

    destDir="$pkgdir/usr/share/catppuccin/$depname"
    install -Ddm755 $destDir

    for flavor in ${flavors[@]}; do
        for accent in ${accents[@]}; do
            install -m644 "$depname/themes/catppuccin-$flavor/catppuccin-$flavor-$accent" $destDir
        done
    done
}
